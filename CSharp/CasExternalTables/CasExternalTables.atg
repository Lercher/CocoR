// note that we don't have a byte order mark here (visual studio code), 
// so we need to use Coco's command line switch -utf8:
// ..\coco CasExternalTables.ATG -frames .. -utf8
// cls & cd .. & build.bat & cd CasExternalTables & cocbuild

COMPILER CasExternalTables

IGNORECASE


CHARACTERS
    ucletter  = 'A'..'Z'.
    lcletter  = 'a'..'z'.
    umlaut    = "ÄÖÜäöüß".
	letter    = ucletter + lcletter + umlaut + '_'.
    digit     = '0'..'9'. 
	cr        = '\r'.
	lf        = '\n'.
	tab       = '\t'.
    quote     = '\''.
    star      = '*'.
	stringCh  = ANY - quote - cr - lf.

TOKENS
    number = ['-'] digit { digit }.
	ident = letter { letter }.
    set : ident = "set".
    serveroutput : ident = "serveroutput".
    on : ident = "on".
    size : ident = "size".
    insert : ident = "insert".
    update : ident = "update".
    delete : ident = "delete".
    into : ident = "into".
    values: ident = "values".    
    prompt : ident = "prompt". 
    null : ident = "null".
    lantusparam : ident = "LANTUSPARAM".
    tusparam : ident = "TUSPARAM".
    tusnom : ident = "TUSNOM".    
    tupcode : ident = "TUPCODE".
    sc = ";".
    openparen = "(".
    closeparen = ")".
    nextblock = "/".
    dot = ".".
    comma = ",".
    equals = "=".
    doublebar = "||".
	string = quote { stringCh | lf | quote quote } quote.
    stars = star { star }.
    

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "--" TO lf

IGNORE cr + lf + tab


SYMBOLTABLES
   languages STRICT "'EN'" "'FR'". 
   deletabletables STRICT "LANTUSPARAM" "TUSPARAM".
   updatetables STRICT "TUSPARAM".
   columns STRICT "TUSNOM" "TUPCODE".
   chrarguments STRICT "38".


PRODUCTIONS

    CasExternalTables =
        SetServeroutput
        nextblock
        { Block }
        EOF
        .

    Block =
        ( ExceptionHandledBlock
        | StarPrompt
        | DeclareBlock
        )
        { nextblock }
        .

    DeclareBlock = 
        "declare"       (. SemErr("Mysterious DECLARE block detected"); .)
        { ANY }
        nextblock
        .

    ExceptionHandledBlock =
        "begin"
            InsertDeleteUpdate { InsertDeleteUpdate }
            ( "commit" sc
            | (. SemErr("COMMIT in ExceptionHandledBlock missing"); .)
            )
            ( PutLine 
            | (. SemErr("INSERT without protocol line"); .)
            )
        "exception" "when" "others" "then"
            [ PutLine ]
            ( "rollback"
            | null
            ) 
            sc            
        "end" sc
        nextblock
        .
    
    InsertDeleteUpdate = 
        ( Insert
        | Delete
        | Update
        )
        .

    // UPDATE TUSPARAM SET TUPFLAGORFI = 1 WHERE TUSNOM = 'GENREHABSTATUS'AND TUPCODE = '05';
    Update =
        update
        ident:updatetables
        "set"
        "TUPFLAGORFI" equals number
        "where"
        ident:columns equals String
        "and"
        ident:columns equals String
        sc
        .

    // DELETE FROM LANTUSPARAM WHERE TUSNOM = 'DEFAULTBVE';
    Delete =
        delete "from"
        ident:deletabletables
        "where"
        ident:columns
        "="
        String
        sc
        .

    Insert =
        "insert" "into"
        ( TUSER
        | LANTUSER
        | TUSPARAM
        | LANTUSPARAM
        )
        .

    TUSER = 
        "TUSER" 
        openparen 
            tusnom comma 
            "TUSLONGUEUR"
        closeparen
        values
        openparen
            String comma
            number
        closeparen
        sc
        .

    LANTUSER = 
        "LANTUSER"
        (
            openparen 
                tusnom comma 
                ( LANTUSER_LANCODE
                | LANTUSER_TUSLIBELLE
                )
        | 
            values 
            openparen
                String         (. SemErr("INSERT INTO LANTUSER without column list: " + t.val); .) 
                comma String
                comma String 
            closeparen
            sc
        ) 
        .

    LANTUSER_LANCODE =
            "LANCODE" comma
            "TUSLIBELLE"
        closeparen
        values
        openparen
            String comma
            string:languages comma
            String
        closeparen
        sc
        .

    LANTUSER_TUSLIBELLE =
            "TUSLIBELLE" comma
            "LANCODE"
        closeparen
        values
        openparen
            String comma
            String comma
            string:languages
        closeparen
        sc
        .


    TUSPARAM = 
        tusparam 
        (
        openparen 
            tusnom 
            comma tupcode
            [ comma "TUPFLAGORFI" ]
        closeparen
        |  (. SemErr("INSERT INTO TUSPARAM without column list"); .)
        )
        values
        openparen
            String 
            comma String 
            [ 
            comma 
            ( number 
            | null 
            | string  (. SemErr("TUPFLAGORFI value " + t.val); .)
            ) 
            ]
        closeparen
        sc
        .

    LANTUSPARAM = 
        lantusparam
        (        
            openparen 
                tusnom comma 
                tupcode comma
                ( LANTUSPARAM_LANCODE
                | LANTUSPARAM_TUPLIBELLE
                )
        |
            values
            openparen
                String          (. SemErr("INSERT INTO LANTUSPARAM without column list: " + t.val); .) 
                comma String
                comma string:languages 
                comma String 
                comma (String | null)
            closeparen
            sc
        )
        .

    LANTUSPARAM_LANCODE =
            "LANCODE" comma
            "TUPLIBELLE"
            [ comma "TUPHELPTEXT" ]
        closeparen
        values
        openparen
            String comma
            String comma
            string:languages comma
            String
            [ comma (String | null) ]
        closeparen
        sc
        .

    LANTUSPARAM_TUPLIBELLE =
            "TUPLIBELLE" comma
            "LANCODE"
        closeparen
        values
        openparen
            String comma
            String comma
            String comma
            string:languages
        closeparen
        sc
        .

    PutLine =
        "dbms_output" dot "PUT_LINE"
        openparen 
            UncheckedString
        closeparen
        sc
        .

    String =
        StringFactor 
        (. 
            if (t.kind == _string && t.val.StartsWith("' ")) {
                SemErr("First string literal starts with a space: " + t.val);
            }
        .)
        { doublebar StringFactor }
        (. 
            if (t.kind == _string && t.val.EndsWith(" '")) {
                SemErr("Last string literal ends with a space: " + t.val);
            }
        .)
        .

    UncheckedString =
        StringFactor 
        { doublebar StringFactor }
        .

    StringFactor (. Console.WriteLine("SF:" + la.val); .)
    =
        string        
        | "chr" openparen number:chrarguments closeparen
        .

    SetServeroutput =
        set serveroutput on size number
        sc
        .

    StarPrompt =
        prompt stars { ANY } stars
        .

END CasExternalTables.
