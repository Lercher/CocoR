// note that we don't have a byte order mark here (visual studio code), 
// so we need to use Coco's command line switch -utf8:
// ..\coco CasExternalTables.ATG -frames .. -utf8
// cls & cd .. & build.bat & cd CasExternalTables & cocbuild

COMPILER CasExternalTables

IGNORECASE


CHARACTERS
    ucletter  = 'A'..'Z'.
    lcletter  = 'a'..'z'.
    umlaut    = "ÄÖÜäöüß".
	letter    = ucletter + lcletter + umlaut + '_'.
    digit     = '0'..'9'. 
	cr        = '\r'.
	lf        = '\n'.
	tab       = '\t'.
    quote     = '\''.
    star      = '*'.
	stringCh  = ANY - quote - cr - lf.

TOKENS
    number = ['-'] digit { digit }.
	ident = letter { letter }.
    set : ident = "set".
    serveroutput : ident = "serveroutput".
    on : ident = "on".
    size : ident = "size".
    insert : ident = "insert".
    into : ident = "into".
    values: ident = "values".    
    prompt : ident = "prompt". 
    null : ident = "null".
    lantusparam : ident = "LANTUSPARAM".
    tusparam : ident = "TUSPARAM".
    tusnom : ident = "TUSNOM".    
    sc = ";".
    openparen = "(".
    closeparen = ")".
    nextblock = "/".
    dot = ".".
    comma = ",".
	string = quote { stringCh | quote quote } quote.
    stars = star { star }.
    

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "--" TO lf

IGNORE cr + lf + tab


SYMBOLTABLES
   languages STRICT "'EN'" "'FR'". 
   deletabletables STRICT "LANTUSPARAM" "TUSPARAM".
   columns STRICT "TUSNOM".


PRODUCTIONS

    CasExternalTables =
        SetServeroutput
        nextblock
        { Block }
        .

    Block =
        ( ExceptionHandledBlock
        | StarPrompt
        | DeclareBlock
        )
        { nextblock }
        .

    DeclareBlock = 
        "declare"       (. SemErr("Mysterious DECLARE block detected"); .)
        { ANY }
        nextblock
        .

    ExceptionHandledBlock =
        "begin"
            InsertDelete { InsertDelete }
            ( "commit" sc
            | (. SemErr("COMMIT in ExceptionHandledBlock missing"); .)
            )
            PutLine
        "exception" "when" "others" "then"
            [ PutLine ]
            ( "rollback"
            | null
            ) 
            sc            
        "end" sc
        nextblock
        .
    
    InsertDelete = 
        ( Insert
        | Delete
        )
        .

    // DELETE FROM LANTUSPARAM WHERE TUSNOM = 'DEFAULTBVE';
    Delete =
        "delete" "from"
        ident:deletabletables
        "where"
        ident:columns
        "="
        string
        sc
        .

    Insert =
        "insert" "into"
        ( TUSER
        | LANTUSER
        | TUSPARAM
        | LANTUSPARAM
        )
        .

    TUSER = 
        "TUSER" 
        openparen 
            tusnom comma 
            "TUSLONGUEUR"
        closeparen
        values
        openparen
            string comma
            number
        closeparen
        sc
        .

    LANTUSER = 
        "LANTUSER"
        (
            openparen 
                tusnom comma 
                ( LANTUSER_LANCODE
                | LANTUSER_TUSLIBELLE
                )
        | 
            values 
            openparen
                string         (. SemErr("INSERT INTO LANTUSER without column list: " + t.val); .) 
                comma string
                comma string 
            closeparen
            sc
        ) 
        .

    LANTUSER_LANCODE =
            "LANCODE" comma
            "TUSLIBELLE"
        closeparen
        values
        openparen
            string comma
            string:languages comma
            string
        closeparen
        sc
        .

    LANTUSER_TUSLIBELLE =
            "TUSLIBELLE" comma
            "LANCODE"
        closeparen
        values
        openparen
            string comma
            string comma
            string:languages
        closeparen
        sc
        .


    TUSPARAM = 
        tusparam 
        (
        openparen 
            tusnom 
            comma "TUPCODE"
            [ comma "TUPFLAGORFI" ]
        closeparen
        |  (. SemErr("INSERT INTO TUSPARAM without column list"); .)
        )
        values
        openparen
            string 
              comma string 
            [ comma ( number | null ) ]
        closeparen
        sc
        .

    LANTUSPARAM = 
        lantusparam
        (        
            openparen 
                tusnom comma 
                "TUPCODE" comma
                ( LANTUSPARAM_LANCODE
                | LANTUSPARAM_TUPLIBELLE
                )
        |
            values
            openparen
                string          (. SemErr("INSERT INTO LANTUSPARAM without column list: " + t.val); .) 
                comma string
                comma string:languages 
                comma string 
                comma (string | null)
            closeparen
            sc
        )
        .

    LANTUSPARAM_LANCODE =
            "LANCODE" comma
            "TUPLIBELLE"
            [ comma "TUPHELPTEXT" ]
        closeparen
        values
        openparen
            string comma
            string comma
            string:languages comma
            string
            [ comma (string | null) ]
        closeparen
        sc
        .

    LANTUSPARAM_TUPLIBELLE =
            "TUPLIBELLE" comma
            "LANCODE"
        closeparen
        values
        openparen
            string comma
            string comma
            string comma
            string:languages
        closeparen
        sc
        .

    PutLine =
        "dbms_output" dot "PUT_LINE"
        openparen string closeparen
        sc
        .

    SetServeroutput =
        set serveroutput on size number
        sc
        .

    StarPrompt =
        prompt stars { ANY } stars
        .

END CasExternalTables.
