COMPILER WFModel

IGNORECASE

CHARACTERS
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  umlaut = "ÄÖÜäöüß".
  germanletter = letter + umlaut.
  underscore = '_'.
  digit     = "0123456789".
  cr        = '\r'.
  lf        = '\n'.
  tab       = '\t'.
  stringCh  = ANY - '"' - cr - lf.
  anybutbrace = ANY - '}'.
  anybutbracket = ANY - ')'.
  dothash = ".#".

TOKENS
  ident = germanletter { germanletter | digit | underscore }.
  dottedident = germanletter { germanletter | digit | underscore } CONTEXT(".") .
  number = ['-'] digit { digit } '.' digit { digit } ['#' | 'R'].
  int = ['-'] digit { digit }.
  string = '"' { stringCh } '"'.
  braced = '{' { anybutbrace  } '}'.
  bracketed = '(' { anybutbracket  } ')'.
  end = "End".
  dot = '.'.
  bar = '|'.
  colon = ':'.
  versionnumber = digit '.' digit '.' digit '.' digit .
  // css = dothash letter { letter | digit | underscore }.  // fights with DottedIdent
  
  // ident tokens :
  version : ident = "version".
  search : ident  = "search".
  select : ident = "select" .
  details : ident  = "details" .
  edit : ident  = "edit" .
  clear : ident  = "clear" .
  keys : ident = "Keys".
  displayname : ident = "Displayname".
  vbident : ident = '[' germanletter { germanletter | digit | underscore } ']'.
  

COMMENTS FROM "'" TO lf 

IGNORE cr + lf + tab


SYMBOLTABLES
	types.
	enumtypes.


PRODUCTIONS

WFModel =
	Version
	Namespace
	[ ReaderWriterPrefix ]
	RootClass
	{
	Class
	| SubSystem
	| Enum
	| Flags
	} 
	EndNamespace
	.	

Version =
	version
	versionnumber
	.

Namespace = 
	SYNC
	"Namespace" 
	DottedIdent 
	.
	
EndNamespace =
	end
	"Namespace"
	.

DottedIdent = 
	dottedident { dot ident } 
	.

ReaderWriterPrefix = 
	SYNC
	"ReaderWriterPrefix"
	ident
	.

RootClass =
	SYNC
	"RootClass"
	"Data"
	Properties
	end "Class"
	.
	
Class =
	SYNC
	"Class" 
	ident>types
	[ Title ]
	[ Inherits ]
	[ Via ]
	Properties
	end "Class"
	.

Via =
	"Via"
	DottedIdent
	.
	
Title =
	braced
	.
	
Inherits =
	"Inherits"
	DottedIdent
	.
	
Properties =
	{
	Prop
	}
	.

Prop =
	SYNC
	(
	Property
	| InfoProperty
	| APProperty
	| List
	| SelectList
	| FlagsList
	| LongProperty
	| InfoLongProperty
	)
	.

Property =
	"Property" ident
	Type
	.

InfoProperty =
	"InfoProperty" ident
	Type
	.

APProperty =
	"APProperty" ident
	Type
	.

List =
	"List" ident
	[ As ]
	.

SelectList =
	"SelectList" ident
	As
	.

FlagsList =
	"FlagsList" ident
	Mimics
	.

LongProperty =
	"LongProperty" ident
	.

InfoLongProperty =
	"InfoLongProperty" ident
	.

Type =
	(
	EmptyType
	| As
	| Mimics
	)
	[ "=" InitValue ]
	[ SampleValue ]
	.

InitValue =
	number 
	| int 
	| string
	| "true"
	| "false"
	| "#" { ANY } "#"
	| FunctionCall
	.

FunctionCall =
	DottedIdent bracketed
	.

SampleValue =
	braced
	.

EmptyType =
	.
	
As =
	"As"
	(
	BaseType
	| ident:types
	| DottedIdent
	)
	.

BaseType =
	"double"
	| "date"
	| "datetime"
	| "integer"
	| "percent"
	| "percentwithdefault"
	| "doublewithdefault"
	| "integerwithdefault"
	| "n2"
	| "n0"
	| "String"
	| "String()"
	.

Mimics =
	"Mimics"
	MimicsSpec
	.

MimicsSpec =
	(
	Query
	| Txt
	| XL
	| Ref
	| ident:enumtypes
	).

Query =
	"query" colon
	ident
	dot
	ident
	colon
	StringOrIdent
	.

Txt =
	"txt" colon
	ident
	dot
	ident
	colon
	StringOrIdent
	.

XL =
	"xl" colon
	ident
	dot
	ident
	colon
	StringOrIdent
	.

Ref =
	"ref" colon
	( keys | displayname )
	colon
	StringOrIdent
	.

StringOrIdent =
	string
	| DottedIdent
	.
	
	
SubSystem = 
	SYNC
	"SubSystem" 
	ident>types
	"SSName" ident
	"SSConfig" ident
	"SSTyp" ident
	"SSCommands" SSCommands
	[ "SSKey" string ]
	[ "SSClear" string ] 
	{ InfoProperty }
	end "SubSystem"
	.

SSCommands = 
	SSCommand 
	{ 
	bar SSCommand 
	}
	.
	
SSCommand = 
	"search" 
	| "select" 
	| "details" 
	| "edit" 
	| "clear"
	.

Flags =
	SYNC
	"Flags"
	ident>types
	{ EnumValue }
	end "Flags"
	.

Enum =
	SYNC
	"Enum" 
	ident>enumtypes
	EnumValues
	end "Enum"
	.

EnumValues =
	{ EnumValue }
	"DEFAULT" EnumValue
	{ EnumValue }
	.

EnumValue = 
	ident
	[ EnumIntValue ]
	.

EnumIntValue =
	"="
	int
	.

END WFModel .